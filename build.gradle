// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    // keep kotlin version sync with compose version
    ext.kotlinVersion = '1.5.30'
    ext.composeVersion = '1.0.3'
    ext.hiltVersion = '2.38.1'
    ext.navVersion = "2.4.0-alpha04"
    repositories {
        google()
        jcenter()
    }

    dependencies {
        // do not use 7.1.0-alpha03! r8 is way too aggressive on the release build in this version
        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath "org.owasp:dependency-check-gradle:6.3.1"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navVersion"
        classpath "com.jaredsburrows:gradle-license-plugin:0.8.90"
    }
}

subprojects {
    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:0.42.1"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "--reporter=html,output=${buildDir}/reports/ktlint.html", "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    piwikVersion = "1.0.1"

    // kotlin
    coroutinesVersion = "1.5.1"

    // base
    fhirVersion = "5.1.0"
    hiltViewModelVersion = "1.0.0-alpha03"
    moshiVersion = "1.12.0"
    okHttpVersion = "4.9.0"
    retrofitVersion = "2.9.0"
    roomVersion = "2.3.0"

    // security
    bouncycastleVersion = "1.69"
    jetSecVersion = "1.1.0-alpha03"
    sqlCipherVersion = "4.4.0"
    safetynetVersion = "17.0.0"

    // location
    locationVersion = "18.0.0"

    // ui
    cameraXVersion = "1.1.0-alpha09"
    cameraXViewVersion = "1.0.0-alpha29"
    lifecycleVersion = "2.3.1"
    materialVersion = "1.3.0"
    mlkitVersion = "17.0.0"
    navVersion = "2.3.5"
    swipeRefreshLayout = "1.1.0"
    videoPlayerVersion = "2.14.0"

    dataStoreVersion = "1.0.0"
}
